plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    // ASSUMES GRADLE 5.6 OR HIGHER. Use plugin version 0.8.10 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.18'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

// sourceCompatibility = 1.8
// targetCompatibility = 1.8

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.47.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.19.2'
def protocVersion = protobufVersion

dependencies {
	implementation 'org.apache.ratis:ratis-server:2.2.0'
    implementation 'org.apache.ratis:ratis-netty:2.2.0'
    implementation 'org.apache.ratis:ratis-grpc:2.2.0'
    implementation 'com.beust:jcommander:1.82'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
	// https://mvnrepository.com/artifact/org.iq80.leveldb/leveldb
	implementation group: 'org.iq80.leveldb', name: 'leveldb', version: '0.12'

    
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    implementation 'com.google.code.gson:gson:2.10.1'
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:3.4.0"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false


task PortalAdministrativoServer(type: CreateStartScripts) {
    mainClass = 'br.ufu.facom.gbc074.projeto.cadastro.PortalAdministrativoServer'
    applicationName = 'portal-administrativo-server'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

task PortalAdministrativoClient(type: CreateStartScripts) {
    mainClass = 'br.ufu.facom.gbc074.projeto.cadastro.PortalAdministrativoClient'
    applicationName = 'portal-administrativo-client'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}
task PortalMatriculaServer(type: CreateStartScripts) {
    mainClass = 'br.ufu.facom.gbc074.projeto.matricula.PortalMatriculaServer'
    applicationName = 'portal-matricula-server'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

task PortalMatriculaClient(type: CreateStartScripts) {
    mainClass = 'br.ufu.facom.gbc074.projeto.matricula.PortalMatriculaClient'
    applicationName = 'portal-matricula-client'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

task ServidorRatis(type: CreateStartScripts) {
    mainClass = 'br.ufu.facom.gbc074.projeto.ratis.Servidor'
    applicationName = 'ratis-server'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

task Teste(type: CreateStartScripts) {
    mainClass = 'br.ufu.facom.gbc074.projeto.teste.GsonTeste'
    applicationName = 'teste'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}


applicationDistribution.into('bin') {
    from(PortalAdministrativoServer)
    from(PortalAdministrativoClient)
    from(PortalMatriculaServer)
    from(PortalMatriculaClient)
    from(ServidorRatis)
    from(Teste)
    fileMode = 0755
}
